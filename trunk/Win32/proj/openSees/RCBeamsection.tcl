# Define a procedure which generates a reinforced concrete beam
# with two layers of rebars at the top and bottom of the beam
# 
#                       y
#                       |
#                       |
#                       |
#                                     
#	       |                   |
#             |   O   O   O   O   |
#             |                   |
#             |                   |
#             |                   |
#  z ---------|                   | 
#             |                   |
#             |                   |
#             |   O   O   O   O   |
#             |                   |
#    
#                 
#
# Formal arguments
#    id - tag for the section that is generated by this procedure
#    h - height of the section
#    w - width of the section
#    cover - thickness of the cover patches
#    coreID - material tag for the core patch
#    coverID - material tag for the cover patch
#    steelID - material tag for the reinforcing steel
#    TnumBars - number of reinforcing bars at the top of the section
#    TbarArea - cross-sectional area of each top reinforcing bar
#    BnumBars - number of reinforcing bars at the bottom of the section
#    BbarArea - cross-sectional area of each bottom reinforcing bar
#    nfbeamY - number of fibers in the beam patch in the y direction
#    nfbeamZ - number of fibers in the beam patch in the z direction
# 

proc RCbeamsection { id   h   w   cover   coreID   coverID	steelID   TnumBars   TbarArea   BnumBars   BbarArea   nfbeamY   nfbeamZ } {


   set beamY [expr $h/2.0]
   set beamZ [expr $w/2.0]
   set nbeamY [expr -$h/2.0]
   set nbeamZ [expr -$w/2.0]

   set coreY [expr 0.5*$h-$cover]
   set coreZ [expr 0.5*$w-$cover]
   set ncoreY [expr -$coreY]
   set ncoreZ [expr -$coreZ]

   # Define the fiber section
   section fiberSec $id {

	# Define the beam core patch
	patch quadr $coreID $nfbeamY   $nfbeamZ   $ncoreY $ncoreZ $coreY $ncoreZ $coreY $coreZ $ncoreY $coreZ
	# Left
	patch quadr $coverID $nfbeamY   $nfbeamZ   $ncoreY $coreZ $coreY $coreZ $coreY $beamZ $ncoreY $beamZ
	# Right
	patch quadr $coverID $nfbeamY   $nfbeamZ   $ncoreY $nbeamZ $coreY $nbeamZ $coreY $ncoreZ $ncoreY $ncoreZ
	# Top
	patch quadr $coverID $nfbeamY   $nfbeamZ   $coreY $nbeamZ $beamY $nbeamZ $beamY $beamZ $coreY $beamZ
	# Bottom
	patch quadr $coverID $nfbeamY   $nfbeamZ   $nbeamY $nbeamZ $ncoreY $nbeamZ $ncoreY $beamZ $nbeamY $beamZ

	# Define the steel along constant values of y (along the z direction)
	layer straight $steelID $TnumBars $TbarArea $coreY $coreZ $coreY $ncoreZ
	layer straight $steelID $BnumBars $BbarArea $ncoreY $coreZ $ncoreY $ncoreZ

   }

}